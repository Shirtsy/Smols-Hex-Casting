#wget(syntax_utils.hexpattern)(https://github.com/Shirtsy/HexLator/raw/main/utils/syntax_utils.hexpattern)

#def(villager_vector)(@vec(816.5,65.5,298.5))
#def(amethyst_vector)(@vec(816.5,66.5,298.5))
#def(item_frame_vector)(@vec(816.5,67.5,298.5))

// Pattern to execute
{
    // Check if first cast
    $if (
        Nullary Reflection
        Muninn's Reflection
        Equality Distillation
    ) then (
        // Ravenmind empty case
        Huginn's Gambit
        Mind's Reflection
        Compass' Purification
        Mind's Reflection
        Alidade's Purification
        Archer's Distillation
        Link
        // Send iota to start up CC program
        Numerical Reflection(0)
        True Reflection
        Send Iota
    ) else (
        // Ravenmind populated case

        // Check if no unread iota
        $if (
            Numerical Reflection(0)
            Postmaster's Reflection
            Equality Distillation
        ) then (
            // Message queue empty case
            Aergia's Gambit
        ) else (
            // Message queue populated case

            $for_each in (Recitation Reflection) do (
                // Stack: pattern
                Gemini Decomposition // pattern, pattern

                // Villager vector
                { $villager_vector } >>
                Entity Purification // pattern, pattern, entity

                // Amethyst vector
                { $amethyst_vector } >> // pattern, pattern, entity, vec

                Numerical Reflection(3)
                Fisherman's Gambit // pattern, entity, vec, pattern
                Gemini Decomposition

                // Item frame vector
                { $item_frame_vector } >>
                Entity Purification
                Jester's Gambit // entity, pattern
                Chronicler's Gambit

                $if (Athena's Gambit) then (
                    // Mishap case
                    Bookkeeper's Gambit(v)
                    Bookkeeper's Gambit(v)
                    Bookkeeper's Gambit(v)
                ) else (
                    // Success case
                    Numerical Reflection(0)
                    Unlink
                    Charon's Gambit
                )
            )
        )
    )
}

// Location
Mind's Reflection
Compass' Purification

// Amount of media
Numerical Reflection(300)

Summon Cyclic Wisp